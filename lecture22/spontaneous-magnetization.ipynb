{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "d45028f6-189d-4182-8293-9571ea1f84c2",
   "metadata": {},
   "source": [
    "\n",
    "# Mean-field theory of spontaneous magnetization "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c9ef3951-0089-4ee7-b884-ba61701847f0",
   "metadata": {},
   "source": [
    "\n",
    "In the framework of mean-field theory, the value of *spontaneous magnetization* of ferromagnetic material \n",
    "is determined by the solution of the following *self-consistency equation*: "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a21b24e9-5919-4a97-a88c-59af40ca4f46",
   "metadata": {},
   "source": [
    "\n",
    "$$m = \\tanh\\left(\\frac{m}{\\tau}\\right). \\qquad\\qquad\\qquad(A)$$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c9ba7377-c84e-485e-be12-76613dc5d9ca",
   "metadata": {},
   "source": [
    "\n",
    "Here $$\\tau = \\frac{T}{T_c},\\qquad\\qquad\\qquad\\qquad\\ $$ \n",
    "$T$ is the temperature, and $T_c$ the characteristic parameter \n",
    "of a ferromagnet which is determined by the geometry of crystall \n",
    "latice and the strength of the spin-spin interaction.\n",
    "\n",
    "For the derivation of Eq. (A) see the class notes at <https://www.phys.uconn.edu/~rozman/Courses/P2200_25F/downloads/mean-field-theory.pdf>"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7d124316-3e4b-4b64-baa4-224dcac85dd4",
   "metadata": {},
   "source": [
    "\n",
    "### Graphical solution of self-consistency equation"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e8bac1da-2f41-4158-88b2-9308dccdf934",
   "metadata": {},
   "source": [
    "\n",
    "Let's investigate the solution of Equation (A). We rewrite the equation in the following form:"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1a26ff96-0e33-44fe-90c7-669f01654387",
   "metadata": {},
   "source": [
    "\n",
    "$$\\tau u = \\tanh\\left(u\\right), \\qquad\\qquad\\qquad(B)$$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "fdd4e247-4fed-4702-8ce9-df29228eabe0",
   "metadata": {},
   "source": [
    "\n",
    "where we introduced new independent variable $u = \\frac{m}{\\tau}$. "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "aab5e698-39c6-4197-b692-b518db3b7837",
   "metadata": {},
   "source": [
    "\n",
    "Let's plot the left and right hands sides of Eq. (B) as functions of $u$ for several values of $\\tau$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "acb5b2ca-d819-473c-9eca-ff9e428d0ca5",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "using PyPlot\n",
    "using Roots"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a391427f-08cc-489b-b434-9be192f9b958",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "# pygui(true)\n",
    "taus = [0.35, 0.5, 0.7, 1.0, 1.5, 3.0]\n",
    "ng = length(taus)\n",
    "us = range(0.0, 3.5, 50)\n",
    "\n",
    "plot(us, tanh.(us), label=L\"\\tanh(u)\", linewidth=2)\n",
    "for i = 1:ng\n",
    "    tau = taus[i]\n",
    "    plot(us, tau .* us, label=L\"\\tau_{%$i}\\,u, \\; \\tau_{%$i} = %$tau\", linestyle=\"dashed\")\n",
    "end\n",
    "ylim(0.0, 1.5)\n",
    "xlabel(\"u\")\n",
    "grid(true)\n",
    "legend();"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f72f0b79-220b-46a2-81ae-acc6a26894dd",
   "metadata": {},
   "source": [
    "\n",
    "We see that if $\\tau > 1$, the graphs of the left and right hands sides of Eq. (B) do not cross (except for trivial solution $u=0$). \n",
    "That means that Eqs. (A) and (B) have only the trivial solution $u = 0$, $m = 0$ if $T > T_c$.  \n",
    "\n",
    "For temperatures below $T_C$, the graphs of the left and right hands sides of Eq. (B) do intersect at $u(T) \\not= 0$. \n",
    "That means that the system is spontaneously magnetized when $T < T_c$."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "46036dac-e7e5-4765-b732-bac6e5d1d04c",
   "metadata": {},
   "source": [
    "\n",
    "### Numerical determination of magnetization m(T)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e674ee8f-7f41-421c-9f26-f984f934e7aa",
   "metadata": {},
   "source": [
    "\n",
    "Let's solve numerically Eq. (A) for spontaneous magnetization."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "781f1c60-3cb9-41af-926c-bb60c7ac8a6e",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "selfconsistency(m, tau) = m - tanh(m/tau)    # m is the independent variable, tau is a parameter"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ac591b03-e5a8-4be9-ba43-8f30973f4a0c",
   "metadata": {},
   "source": [
    "\n",
    "Let's solve for $\\tau_{\\mathrm{min}} \\le \\tau \\le \\tau_{\\mathrm{max}}$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b19a76cf-aaf9-4fd2-aab8-eac07448706e",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "taumin = 0.2\n",
    "taumax = 1.0\n",
    "np = 201;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "84df3dde-2855-493c-bbed-5bcb632d5f35",
   "metadata": {},
   "source": [
    "\n",
    "Pre-allocate storage:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8ee8b727-9dde-48e7-b096-d29bd4f37ae3",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "tau = range(taumin, taumax, np)\n",
    "mag = zeros(np);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2d23fe2f-0be8-4e00-9a19-c3441045e77a",
   "metadata": {},
   "source": [
    "\n",
    "Find spontaneous magnetization vs temperature"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b14d4db4-d2a1-4b67-92be-02e44654f88b",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "for i = 1:np-1\n",
    "    mag[i] = find_zero(selfconsistency, (0.001, 1.0), tau[i]) # use bisection\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "011f3c37-d64d-41d4-ac4c-92f7866f1575",
   "metadata": {},
   "source": [
    "\n",
    "Plot spontaneous magnetization:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fcd97757-f399-48b1-8269-a2766ac4a43c",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "plot(tau, mag)\n",
    "plot([1.0, 1.6], [0.0, 0.0], color=\"#1f77b4\") # zero magnetization for tau >= 1, i.e. for T > T_c\n",
    "grid(true)\n",
    "xlabel(L\"T/T_c\")\n",
    "ylabel(L\"\\langle s \\rangle\")\n",
    "title(\"Spontaneous magnetization in ferromagnet\");"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f4a3658f-b027-4e9e-bb94-c58b3d1412b5",
   "metadata": {},
   "source": [
    "\n",
    "### Critical exponent"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "681efa82-f98e-4cd9-b3a5-8a0068cedd7b",
   "metadata": {},
   "source": [
    "\n",
    "Let's look the the magnetization $m(T)$ in the close vicinity of the phase transition."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4eac6e7b-6059-461b-884a-f1ed48748159",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "ind = 190:np-1;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c7ecdbaf-17d5-4a96-84cf-50b63edf29d0",
   "metadata": {},
   "source": [
    "\n",
    "The variable that describes $T - T_c$:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a8ff9e26-b425-4309-aa16-87236856235a",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "q = 1.0 .- tau;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "200ccd87-55f4-4aa1-9ce9-a2e947712c5b",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "loglog(q[ind], mag[ind], marker=\".\", label=\"mean field theory\")\n",
    "loglog(q[ind], 1.4*(q[ind]).^0.5, linestyle=\"dashed\", label=L\"(1 - T/T_c)^{1/2}\")\n",
    "xlabel(L\"1 - T/T_c\")\n",
    "ylabel(\"magnetization\")\n",
    "grid(true)\n",
    "legend();"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "11168023-a451-451b-8db4-9cbf3dbcdf1e",
   "metadata": {},
   "source": [
    "That is, for $T < T_c$,\n",
    "\n",
    "$$m(T)  \\sim \\left(1 - \\frac{T}{T_c}\\right)^{\\frac{1}{2}} .$$\n",
    "\n",
    "This power dependence is belived to be universal. The exponent $\\frac{1}{2}$ is called *critical exponent*."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "284cdea7-6215-48ad-bf32-749551bc4879",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
