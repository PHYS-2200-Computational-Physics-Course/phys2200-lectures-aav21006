{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "2c99b569-aedd-47d3-8e62-f629609d5f39",
   "metadata": {},
   "source": [
    "\n",
    "# Solving nonlinear equations"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f919b720-3835-4173-a5c4-23bd7213922b",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "using PyPlot"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "707d71a0-dbdc-4f5c-8874-72956763d48f",
   "metadata": {},
   "source": [
    "\n",
    "The test equation:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "904266ac-fe1b-4685-9366-05aa1ae79a12",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "f(x) = exp(-x) - x"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "151301a0-9316-4766-9588-4636eb1406b9",
   "metadata": {},
   "source": [
    "\n",
    "Let's plot a graph of f(x)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "de45b2a8-e771-4620-8c98-552195316360",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "xmin = -1.0\n",
    "xmax = 3.0\n",
    "np = 100\n",
    "x = range(xmin, xmax, np)\n",
    "y = f.(x);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a333e38a-dc0b-404f-a34b-85eb52f7a79a",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "plot(x, y, label=L\"f(x) = e^{-x} - x\")\n",
    "axhline(color=\"black\")\n",
    "grid(true)\n",
    "xlabel(\"x\")\n",
    "ylabel(\"f(x)\")\n",
    "title(\"An example of nonlinear equation f(x) = 0\")\n",
    "legend();"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cadc13ee-67cc-4c3f-afae-296f1f1c403a",
   "metadata": {},
   "source": [
    "\n",
    "By visual inspection: the root of nonlinear equation is on the interval (0.0, 1.0)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8b404e44-d391-4e7d-9c6d-2f91bade08a9",
   "metadata": {},
   "source": [
    "\n",
    "## Bracketing methods. Bisection algorithm.\n",
    "\n",
    "For a function $f(x)$ (univariate, real-valued) a bracket is a pair $a,b$,\n",
    "$a<b$ for which $f(a)f(b)<0$. That is the function values have different signs at \n",
    "a and b. If $f(x)$ is a continuous function this ensures there will be a zero in \n",
    "the interval $[a,b]$. If $f(x)$ is not continuous, then there must be a point \n",
    "$c \\in [a,b]$ where the function \"jumps\" over 0.\n",
    "\n",
    "Such values can be found, up to floating point round off. That is, given `f(a) * f(b) < 0`, \n",
    "a value $c$ with $a < c < b$ can be found where either `f(c) == 0.0` or `f(prevfloat(c)) * f(c) < 0` \n",
    "or `f(c) * f(nextfloat(c)) < 0`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d5f78520-8226-4b77-8d40-1ec25caae8e7",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "\"\"\"\n",
    "    rt, ni = mybisection(f, a, b, abstol)\n",
    "\n",
    "Solve nonlinear equation f(x) = 0 using bisection. a and b are the endpoints of the initial bracketing interval. \n",
    "abstol is the acceptble error of the solution.\n",
    "\n",
    "mybisection() return the approximation for the root of the equation f(x) = 0 and the number of required function calls.\n",
    "\"\"\"\n",
    "function mybisection(f, a, b, abstol)\n",
    "    fa = f(a)\n",
    "    fb = f(b)\n",
    "    sa = sign(fa)\n",
    "    sb = sign(fb)\n",
    "    if sa == sb\n",
    "        error(\"The provided interval [$a,$b] does not contain a root of the equation f(x)=0\")\n",
    "    end\n",
    "    ni = 0\n",
    "    while abs(b - a) > abstol\n",
    "        ni += 1\n",
    "        m = a + (b - a)/2\n",
    "        fm = f(m)\n",
    "        sm = sign(fm)\n",
    "        if sm == sb\n",
    "            b = m\n",
    "            sb = sm\n",
    "        else\n",
    "            a = m\n",
    "            sa = sm\n",
    "        end\n",
    "    end\n",
    "    return a, ni\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "78ce4b47-8f33-49d3-822f-e8bf4dfbe400",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "xb, nb = mybisection(f, 0.0, 1.0, eps()/2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "26b8f3a4-8962-45ca-ab7c-0fa8e227c743",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "f(xb) ≈ 0.0"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "77f837bc-c68f-4536-bfdf-05b0503d3847",
   "metadata": {},
   "source": [
    "\n",
    "Bracketing methods have **guaranteed convergence**, but in general may require many more \n",
    "function calls than other types of root finding algorithms."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "83d362f6-5012-4c58-91c6-5257e0c9cf8c",
   "metadata": {},
   "source": [
    "\n",
    "## Non-bracketing methods. Newton's method\n",
    "\n",
    "If a good initial guess is known, then different types of iterative algorithms \n",
    "are more efficient. However, unlike bracketing methods, these algorithms may \n",
    "not converge to the desired root or not converge at all."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "09f36f93-de14-4582-919c-c8feafb1f18a",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "\"\"\"\n",
    "    rt, ni = mynewtons(f, fp, xin, abstol, itmax)\n",
    "\n",
    "Solve nonlinear equation f(x) = 0 using Newton's method. fp(x) = f'(x). xin is initial approximaption to the root. \n",
    "abstol is the acceptble error of the solution. itmax is the maximal number of iterations.\n",
    "\n",
    "mynewtons() return the approximation for the root of the equation and the number of iterations.\n",
    "\"\"\"\n",
    "function mynewtons(f, fp, xin, abstol, itmax)\n",
    "    x = xin\n",
    "    fx = f(x)\n",
    "    fpx = fp(x)\n",
    "    delta = - fx/fpx\n",
    "    ni = 0\n",
    "    while abs(delta) > abstol\n",
    "        ni += 1\n",
    "        if ni == itmax\n",
    "            break\n",
    "        end\n",
    "        x += delta\n",
    "        fx = f(x)\n",
    "        fpx = fp(x)\n",
    "        delta = - fx/fpx\n",
    "    end\n",
    "    return x, ni\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bafcb5f5-da26-41d6-b85c-59b0fc6f85eb",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "fp(x) = -exp(-x) - 1.0"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3d8043d9-7734-4e40-ab28-102ee806c6ec",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "itmax = 55;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d09d981e-b637-4cc5-b1d6-23ed31cf1d23",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "xn, nn = mynewtons(f, fp, 1.0, eps()/2, itmax)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "65d938f1-daae-4e73-930c-ab6ad3a2f800",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "xn ≈ xb"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "81805199-d37b-4143-8f06-2fab20772b1e",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "f(xn) ≈ 0.0"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8346dc1f-0418-47c3-b9b5-8b48f60440c2",
   "metadata": {},
   "source": [
    "\n",
    "### An example of a nonlinear equation where Newton's method always fails:"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cc456583-840c-4090-929a-e761b89d509b",
   "metadata": {},
   "source": [
    "\n",
    "$$f(x) = \\mathrm{sign}(x - 1) \\, \\sqrt{|x - 1|}$$\n",
    "$$\\frac{\\mathrm{d}f}{\\mathrm{d}x} = \\frac{1}{2 \\, \\sqrt{|x - 1|}}$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "361617a2-26b8-4888-94f1-82bcbfc5e397",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "ff(x) = sign(x - 1.0) * sqrt(abs(x - 1.0))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6af097b9-cd4c-430e-9a5f-26117cb6e9f6",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "umin = -3.0\n",
    "umax = 5.0\n",
    "npf = 500\n",
    "u = range(umin, umax, npf);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bf2b980a-8b55-4ad0-9ca8-1c8b72593745",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "plot(u, ff.(u), label=\"ff(u)\")\n",
    "axhline(color=\"black\")  # yzero axis\n",
    "grid(true)\n",
    "xlabel(\"u\")\n",
    "ylabel(\"ff(u)\")\n",
    "title(\"An example of a 'difficult' nonlinear function\")\n",
    "legend();"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "24e7b9c3-d300-46d1-9ade-a1611fbc04d2",
   "metadata": {},
   "source": [
    "There is an isolated zero at u = 1.0"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6e938f9b-a84f-45cd-bb84-e90740afd327",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "ffp(x) = 1/(2*sqrt(abs(x - 1.0)))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ede9cf43-58fc-45f8-9519-f63e1641cebd",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "uf, nf = mynewtons(ff, ffp, 1.1, eps(), itmax)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5ebf37d6-ab3f-455d-bde4-4071a9616261",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "uf, nf = mynewtons(ff, ffp, 1.01, eps(), itmax)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d8ccb40b-dc45-4686-912b-c9382101bf83",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "uf, nf = mynewtons(ff, ffp, 11.0, eps(), itmax)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b10491f7-194f-4ca0-adb9-9a7d4ca1f674",
   "metadata": {},
   "source": [
    "\n",
    "Bisection method has no troubles finding the root:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "17b60727-b145-4602-9ce5-cb5351868307",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "xf, nf = mybisection(ff, 0.0, 2.0, eps())"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4d7802fc-ebe0-4a7a-bf9e-9c3cb35253f0",
   "metadata": {},
   "source": [
    "\n",
    "### An example of a nonlinear equation where Newton's method 'sometimes' fails:"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d0c6f282-bf0c-44e1-b788-d149eeffab1f",
   "metadata": {},
   "source": [
    "\n",
    "$$f(x) = \\frac{x - 1}{(x - 1)^2 + 1}$$\n",
    "$$\\frac{\\mathrm{d}f}{\\mathrm{d}x} = \\frac{1 - (x - 1)^2}{((x - 1)^2 + 1)^2}$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "48df94f4-d30a-4689-ab89-6bb6e7fb54f2",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "ffs(x) = (x - 1.0)/((x-1.0)^2 + 1.0)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6d7496e8-e9ed-4543-ba4c-6d30280820c3",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "plot(u, ffs.(u), label=\"ffs(u)\")\n",
    "plot([umin, umax], [0.0, 0.0], linewidth=2, color=\"black\")  # yzero axis\n",
    "grid(true)\n",
    "xlabel(\"u\")\n",
    "ylabel(\"ffs(u)\")\n",
    "title(\"An example of a 'difficult' nonlinear function\")\n",
    "legend();"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ccf47af9-8497-4b43-830b-039c1c086272",
   "metadata": {},
   "source": [
    "\n",
    "There is an isolated zero at u = 1.0"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d6c217da-6521-4de8-913b-13fcbfde5b9c",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "ffsp(x) = (1.0 - (x - 1.0)^2) /((x-1.0)^2 + 1.0)^2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6c48488b-3e70-4b8f-97b7-97d5719de510",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "ufs, nfs = mynewtons(ffs, ffsp, 3.0, eps(), itmax)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6d72ff24-3ee3-4a3b-8661-52fcd5a4f57c",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "ufs, nfs = mynewtons(ffs, ffsp, -1.0, eps(), itmax)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7720df1a-a35d-4e83-a519-f2364bbb9eba",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "ufs, nfs = mynewtons(ffs, ffsp, 0.5, eps(), itmax)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "521e48aa-0774-4d9a-a7d4-177d8dadfe89",
   "metadata": {},
   "source": [
    "Bisection method has no troubles finding the root:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "aa4b7b9c-3448-4c87-a75a-7886db0edab5",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "xfs, nfs = mybisection(ffs, 0.0, 2.0, eps())"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8cb323be-2f80-4cf8-b739-9c6618ca1796",
   "metadata": {},
   "source": [
    "\n",
    "## Julia package `Roots` for solving nonlinear equations"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e884ed97-e005-4f12-a01f-eae44decaa46",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "using Roots"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "6c1b51b2-bc6d-4fc3-b8be-ae35099298d0",
   "metadata": {},
   "source": [
    "\n",
    "A test function with several roots:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2bc15b94-d7aa-47ef-97e4-6700a4b8bc6d",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "f2(x) = x^5 - x + 1/4"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "66b7defb-4d85-4fa2-a5b8-6404a03e4a99",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "np = 50\n",
    "s = range(-1.2, 1.2, np)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6897b81f-03e4-429f-b1c3-57ad74d47137",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "plot(s, f2.(s))\n",
    "axhline(color=\"black\")\n",
    "title(\"Test function\")\n",
    "xlabel(\"x\")\n",
    "ylabel(\"f(x)\")\n",
    "grid(true)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "56f9e102-b114-410e-8c2a-3938b586cb0a",
   "metadata": {},
   "source": [
    "\n",
    "#### The solver uses a bracketing algorithm when the initial data is passed as a tuple:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "82322548-bf82-4767-9917-7f7c731a0184",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "r1 = find_zero(f2, (-1.2,  -1.0), verbose=true)    # optional keyword `verbose`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a9cb17ee-c37f-492a-bdef-9aabbf70378a",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "scatter(r1, f2(r1), marker=\"o\", label=\"root\")\n",
    "plot(s, f2.(s))\n",
    "axhline(color=\"black\")\n",
    "title(\"The result of bisection algorithm\")\n",
    "xlabel(\"x\")\n",
    "ylabel(\"f(x)\")\n",
    "legend()\n",
    "grid(true)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a1262086-4ad4-469b-b1ac-c63dea7f67d4",
   "metadata": {},
   "source": [
    "\n",
    "#### Passing just an initial guess will dispatch to a non-bracketing method."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3fa9f0ff-9d9c-478c-964f-feb04b98176b",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "r2_0 = 0.6\n",
    "r2 = find_zero(f2,  r2_0, verbose=true)\n",
    "\n",
    "r3_0 = 1.1\n",
    "r3 = find_zero(f2,  r3_0);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f99d3a7b-c705-4b6d-b695-3a7c078e4f47",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "scatter([r2_0, r2], [f2(r2_0),f2(r2)], marker=\"o\")\n",
    "scatter([r3_0, r3], [f2(r3_0),f2(r3)], marker=\"o\")\n",
    "\n",
    "plot(s, f2.(s))\n",
    "axhline(color=\"black\")\n",
    "title(\"The results of a non-bracketing algorithm\")\n",
    "xlabel(\"x\")\n",
    "ylabel(\"f(x)\")\n",
    "grid(true)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "aeadbb83-0cc8-41e8-8557-a505e5ad6993",
   "metadata": {},
   "source": [
    "\n",
    "#### It is possible to find all zeros in a given interval:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "63eea2d5-9ec5-41aa-b098-875253f9d5bf",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "zs = find_zeros(f2, -1.2, 1.2)\n",
    "scatter(zs, f2.(zs), marker=\"o\", label=\"all roots in the interval [-1.2,1.2]\")\n",
    "\n",
    "plot(s, f2.(s))\n",
    "axhline(color=\"black\")\n",
    "title(\"Finding multiple zeros at once\")\n",
    "xlabel(\"x\")\n",
    "ylabel(\"f(x)\")\n",
    "legend()\n",
    "grid(true)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "adf3ab29-9e56-43ac-95c7-3d8aa6605e98",
   "metadata": {},
   "source": [
    "\n",
    "#### Functions can have parameters\n",
    "\n",
    "The values of parameters can be passed to `find_zero`."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9c5bd5f8-177d-4016-890c-c6a57555e375",
   "metadata": {},
   "source": [
    "\n",
    "The following is a function that has a root $x = 0$ when $p = 0$.\n",
    "\n",
    "We use (-1/2, 1/2) as a bracket:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "094e602a-6eb0-4287-8460-65092835bbea",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "fp(x, p) = x^5 - x + p\n",
    "\n",
    "p = 0.0\n",
    "\n",
    "r = find_zero(fp, (-1/2, 1/2), p)     # <= passing the value of a parameter to the solver\n",
    "\n",
    "plot(s, fp.(s, p))\n",
    "scatter(r, fp(r, p), marker=\"o\", label=\"root\")\n",
    "axhline(color=\"black\")\n",
    "title(\"Finding a root of a parameter-dependent function\")\n",
    "xlabel(\"x\")\n",
    "ylabel(\"f(x)\")\n",
    "legend()\n",
    "grid(true)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7bdada92-7873-478c-9c99-d1e1733b65d0",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
